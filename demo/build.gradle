buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

group = 'com.example'
version = '0.0.1-SNAPSHOT'

description = """demo"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
     maven { url "http://repo.maven.apache.org/maven2" }
}

bootRun {
    // pass gradle's system properties to boot app.
    systemProperties System.properties

    // Load templates from local file system on local development.
    systemProperty "spring.freemarker.templateLoaderPath", [
            "file:${project.projectDir}/src/main/resources/templates/",
            "classpath:/templates/"
    ].join(",")

    // -PjvmArgs='...'
    if ( project.hasProperty('jvmArgs') ) {
        jvmArgs project.jvmArgs.split('\\s+')
    }
}

task benchmark(type: org.springframework.boot.gradle.run.BootRunTask) {
    doFirst {
        systemProperties System.properties
        jvmArgs = [
                "-server",
                "-Djava.net.preferIPv4Stack=true",
                "-Dnetworkaddress.cache.ttl=0",
                "-Dnetworkaddress.cache.negative.ttl=0",
                "-XX:OnOutOfMemoryError=\"kill -9 %p\"",
                "-XX:+OptimizeStringConcat",
                "-verbose:gc"
        ]
        main = "com.example.DemoApplication"
        classpath = sourceSets.main.runtimeClasspath
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-actuator-docs')
    compile('org.springframework.boot:spring-boot-starter-freemarker')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jetty')
    compile('org.springframework.boot:spring-boot-configuration-processor')

    compile('com.google.guava:guava:19.0')

    compile('org.projectlombok:lombok:1.16.6')
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:'1.3.3.RELEASE') {
        exclude(module: 'commons-logging')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
